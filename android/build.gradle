plugins {
    id "com.android.library"
    id "kotlin-android"
}

def localProperties = new Properties()
def localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) {
    localPropsFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

rootProject.ext {
    if (!has('compileSdkVersion')) compileSdkVersion = 33
    if (!has('targetSdkVersion')) targetSdkVersion = 33
    if (!has('javaVersion')) javaVersion = JavaVersion.VERSION_17
    if (!has('kotlinJvmTarget')) kotlinJvmTarget = "17"
    if (!has('kotlinVersion')) kotlinVersion = "1.8.22"
    if (!has('desugarVersion')) desugarVersion = "2.1.5"
    if (!has('exifInterfaceVersion')) exifInterfaceVersion = "1.3.6"
}

android {
    namespace "camera_gps_plugin"
    compileSdkVersion rootProject.ext.compileSdkVersion

    if (rootProject.ext.has("ndkVersion")) {
        ndkVersion rootProject.ext.ndkVersion
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    compileOptions {
        sourceCompatibility = rootProject.ext.javaVersion
        targetCompatibility = rootProject.ext.javaVersion
        coreLibraryDesugaringEnabled = true
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
        debug {}
    }
}

kotlin {
    jvmToolchain {
        languageVersion = JavaLanguageVersion.of(rootProject.ext.kotlinJvmTarget.toInteger())
    }
}

dependencies {
    def kotlinVersion = rootProject.ext.kotlinVersion
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    def exifVersion = rootProject.ext.exifInterfaceVersion
    implementation "androidx.exifinterface:exifinterface:$exifVersion"

    def desugarVersion = rootProject.ext.desugarVersion
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugarVersion"
}